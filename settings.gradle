import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories {
        // For plugin dependencies.
        maven { url "./local-repo" }
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}


gradle.beforeProject { Project p->
    if (p != rootProject){
        p.configurations.all {Configuration cfg->
            cfg.resolutionStrategy.dependencySubstitution{ substitute->
                substitute.substitute(module("me.yangxiaobin:kcp-kt-debuglog:0.0.1")).with (project(":kcp-kt-compiler-debuglog"))
                substitute.substitute(module("me.yangxiaobin:kcp-kt-module-instrument:0.0.1")).with (project(":ksp-kt-compiler-module-instrument"))
            }
        }
    }
}

dependencyResolutionManagement {
    // module 声明 repo 会报错
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    //repositoriesMode.set(RepositoriesMode.PREFER_PROJECT)
    defaultLibrariesExtensionName.set('deps')
    repositories {
        // Query local first.
        maven { url "./local-repo" }
        mavenLocal()

        google()
        mavenCentral()
        jcenter() // Warning: this repository is going to shut down soon

        // For hms scan kit
        maven { url 'https://developer.huawei.com/repo/' }
    }
}
rootProject.name = "AndroidCodelab"
enableFeaturePreview('VERSION_CATALOGS')

includeBuild 'kcp-gradle-plugin'
include ':android-ksp'

include ':app'
include ':codelab-kt'
include ':codelab-android-kt'
include ':kt-compose-lib'

include ':network-lib'
include ':multi-process'
include ':android-colors'
include ':android-permission'
include ':common-ui'
include ':json-mediator'

include ':android-qrcode'
include ':android-qrcode-impl'

include ':module-service-provider-annotation'
include ':module-service-provider'
include ':kcp-kt-compiler-debuglog'
include ':ksp-kt-compiler-module-instrument'
include ':proguard-lib'
include ':nav-lib'
include ':permission-example'
include ':jank-sample'
